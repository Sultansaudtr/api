== Naming rule

=== OGC name schemes

Two URI schemes [IETF RFC 3986] are defined by OGC to provide persistent names for resources of interest in geographic information infrastructures. An http Uniform Resource Identifier(URI) scheme [IETF RFC 4395] provides identifiers which may be resolved to resource representations using the standard web infrastructure, in particular the Domain Name System (DNS) system. A Uniform Resource Name (URN) scheme [IETF RFC 2141] provides equivalent identifiers which do not imply location or a specific resolution mechanism.

Http URI and URN schemes use different delimiters for the elements within the identifier ('/' and ':' respectively). Rules for the names that allow either form to be composed from the same elements requires that the elements use a reduced character set which excludes both of these delimiters.

[NOTE]
====
The URN scheme is essentially identical to that described in version 1 of this policy. The ABNF description below has been revised for consistency with [IETF RFC 3986] and to accommodate the URN/URI consideration above. Although the rule here excludes some identifier forms that may have previously been valid, in practice no assignments were made using the characters now excluded so there is no backward compatibility problem.
====

The following ABNF adapted from [IETF RFC 3986] provides some basic definitions required in the rest of this document, and in other OGC-NA policy documents:

  segment       = *pchar
  segment-nc    = *pchar-nc
  segment-nz    = 1*pchar
  segment-nz-nc = 1*pchar-nc
  pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"
  pchar-nc      = unreserved / pct-encoded / sub-delims / "@"
  pct-encoded   = "%" HEXDIG HEXDIG
  unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~"
  reserved      = gen-delims / sub-delims
  gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@"
  sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
  / "*" / "+" / "," / ";" / "="

=== Use of name structure

The naming schemes defined by OGC-NA in this and related documents have a regular structure. The structure is primarily to provide the name provider (OGC) with a mechanism to govern the names and ensure uniqueness. While name users (clients) might attempt to infer other names based on the structure, the OGC-NA registers are the normative source of OGC names.

=== http URI scheme

The generic syntax for OGC http URIs is

  URI = "http://www.opengis.net/" OGCResource "/" ResourceSpecificPath

=== URN scheme

The generic syntax for OGC URNs is [IETF RFC 5165]

  URN = "urn:ogc:" OGCResource ":" ResourceSpecificString

=== Resource type and Name Type Specification

OGCResource is a token that indicates the resource type, taken from the register  http://www.opengis.net/register/ogc-na/type.

The special resource types "Resource type", "OGC-NA register" and "Name authority" are defined in this document. Each other resource type is defined in an OGC-NA policy document known as a "Name Type Specification" (NTS).

An OGC name for a resource type shall be produced using the following rule:

  OGCResource   = "type"
  ResourceSpecificPath = "ogc-na" "/" type
  ResourceSpecificString = "OGC-NA" ":" type
  type          = segment-nz-nc
                  ; a token from the register of OGC resource types

Note that the type register is available from http://www.opengis.net/register/ogc-na/type.

EXAMPLE 1: http://www.opengis.net/type/ogc-na/def

EXAMPLE 2: urn:ogc:type:OGC-NA:def

Changes to the register of resource types (addition, deletion, supercession) shall be by submission of a proposed NTS to the OGC Naming Authority names@opengeospatial.org.

An NTS shall contain the following:

. A description of a class of resources which may be designated with OGC Names,
. The token to be used as the value of OGCResource
. A description of the structure of the ResourceSpecificPath and ResourceSpecificString for this resource type.  NOTE: If the ResourceSpecifcPath and ResourceSpecificString refer to items from other registers, details of these must be provided in the NTS. These registers may be controlled by OGC or the OGC-NA, or may be controlled by an external authority.
. Details of any type-specific policies for assignment of OGC Names NOTE: This provides the information required for OGC-NA to assess requests for registration of names for this resource type

=== OGC-NA Register

An OGC-NA register is a set of items required for the assignment and management of OGC names.
An OGC URN for an OGC-NA register shall be produced using the following rule:

  OGCResource   = "register"
  ResourceSpecificPath = "ogc-na"  "/" register
  ResourceSpecificString = "OGC-NA"  ":" register
  register      = segment-nz-nc

EXAMPLE 1: http://www.opengis.net/register/ogc-na/def-type
EXAMPLE 2: urn:ogc:register:OGC-NA:def-type OGC-NA registers are created from time to time by OGC-NA for the purpose of managing OGC Name assignment.

=== Name authority

A Name Authority is an organization, specification document or service that is authorized by OGC-NA to provide or describe resources of designated resource types or sub-types. The set of Name authorities is provided by the register at  http://www.opengis.net/register/ogc-na/authority.  An OGC URN for a Name authority shall be produced using the following rule:

  OGCResource   = "auth"
  ResourceSpecificPath = "ogc-na" "/" authority
  ResourceSpecificString = "OGC-NA" ":" authority
  authority     = segment-nz-nc
                  ; a token from the register of OGC Authorities

Note that the authority register is available at http://www.opengis.net/register/ogc-na/authority/

EXAMPLE 1: http://www.opengis.net/auth/ogc-na/EPSG
EXAMPLE 2: urn:ogc:auth:OGC-NA:EPSG

Changes to this register (additions, deletions, supercession) shall be by submission of a Name Authority Proposal to the OGC Naming Authority names@opengeospatial.org.

A Name Authority Proposal shall contain the following:

. A description of, or locator for, the Name Authority
. The token to be used to indicate this authority where required in an OGC Name
. The resource type(s) (including sub-types where appropriate) that this authority is responsible for
. Details of any authority-specific policies for name assignment (e.g. rules for name element variables)
